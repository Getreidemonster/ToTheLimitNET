sub "pullGitHubRepo":
	set {_servertype} to "%parameter 1%"

	add "!!DatabaseIO.sk" to {_gitfiles::*}
	add "!Locations.sk" to {_gitfiles::*}
	add "Essentials.sk" to {_gitfiles::*}
	add "%{_servertype}%/!!!%{_servertype}%_Scoreboard.sk" to {_gitfiles::*}
	
	if {_servertype} = "Lobby":
		add "%{_servertype}%/!KosmetikBuch.sk" to {_gitfiles::*}
		add "%{_servertype}%/AutoNicker_Core.sk" to {_gitfiles::*}
		add "%{_servertype}%/Gadgets.sk" to {_gitfiles::*}
		add "%{_servertype}%/LobbySystem.sk" to {_gitfiles::*}
		add "%{_servertype}%/Navigator.sk" to {_gitfiles::*}
	else:
		add "AutoNicker_Addon.sk" to {_gitfiles::*}
		add "Back2Lobby.sk" to {_gitfiles::*}
		add "Stats.sk" to {_gitfiles::*}
		add "Troll.sk" to {_gitfiles::*}
		# add "%{_servertype}%/NetworkVariables.sk" to {_gitfiles::*}

	if {_servertype} = "SkyWars":
		add "%{_servertype}%/!SkyWarsKits.sk" to {_gitfiles::*}
		add "%{_servertype}%/SkyWars.sk" to {_gitfiles::*}
		add "%{_servertype}%/SkyWars_KitAbilities.sk" to {_gitfiles::*}
		add "%{_servertype}%/SkyWars_RandomChestLoot.sk" to {_gitfiles::*}

	if {_servertype} = "GunGame":
		add "%{_servertype}%/GunGame.sk" to {_gitfiles::*}

	set {_scriptfiles::*} to dir "plugins\Skript\scripts\"'s files
	loop {_scriptfiles::*}:
		set {_} to loop-value
		replace all "plugins\Skript\scripts\" with "" in {_}
		if {_} contains "!!GitHub.sk":
			remove loop-value from {_scriptfiles::*}
		else:
			set {_scriptfiles::%loop-index%} to relative path of {_}

	loop {_scriptfiles::*}:
		console command "sk disable %loop-value%"
		delete file "plugins\Skript\scripts\-%loop-value%"

	loop {_gitfiles::*}:
		set {_url} to "https://raw.githubusercontent.com/Getreidemonster/ToTheLimitNET/master/%loop-value%"
		if loop-value contains {_servertype}:
			set {_a::*} to loop-value split at "/"
			set {GitHub::File::%loop-value%} to {_a::2}
		else:
			set {GitHub::File::%loop-value%} to loop-value
		download file from "%{_url}%" to file "plugins/Skript/scripts/%{GitHub::File::%loop-value%}%"
		if file "plugins\Skript\scripts\%{GitHub::File::%loop-value%}%" exists:
			console command "sk reload %{GitHub::File::%loop-value%}%"

		delete {GitHub::File::%loop-value%}

function getServerType() :: text:
	set {_properties::*} to file contents of "server.properties"
	loop {_properties::*}:	
		set {_} to loop-value
		if "%{_}%" contains "server-type=":
			set {_a::*} to {_} split at "="
			return {_a::2}
	return "null"

load:
	invoke "pullGitHubRepo" from getServerType()
unload:
	invoke "pullGitHubRepo" from getServerType()

